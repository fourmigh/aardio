import win.ui
import console
import fsys.dlg
import web.json
import win.inputBox
/*DSG{{*/
mainForm = win.form(text="Languages.CFG Editor V1.1";right=849;bottom=599;)
mainForm.add(
btnLangResSave={cls="button";text="Save";left=712;top=182;right=794;bottom=250;z=10;};
btnLanguageSave={cls="button";text="Save";left=593;top=19;right=675;bottom=87;z=7;};
btnLoad={cls="button";text="Load";left=701;top=19;right=794;bottom=87;z=12;};
btnNewLanguage={cls="button";text="New Language";left=112;top=104;right=272;bottom=131;z=16;};
btnNewResKey={cls="button";text="New ResKey";left=112;top=467;right=272;bottom=494;z=17;};
etLangResContent={cls="edit";left=302;top=182;right=681;bottom=322;edge=1;multiline=1;z=11;};
etLanguageImageName={cls="edit";left=378;top=62;right=533;bottom=87;edge=1;multiline=1;z=6;};
etLanguageTitle={cls="edit";left=378;top=19;right=533;bottom=44;edge=1;multiline=1;z=4;};
etLanguageWelcome={cls="edit";left=378;top=103;right=533;bottom=128;edge=1;multiline=1;z=15;};
lbLanguages={cls="listbox";left=112;top=19;right=272;bottom=87;edge=1;items={};vscroll=1;z=2;};
lbResKey={cls="listbox";left=112;top=182;right=272;bottom=449;edge=1;hscroll=1;items={};vscroll=1;z=9;};
static={cls="static";text="LanguageList";left=16;top=19;right=90;bottom=82;transparent=1;z=1;};
static2={cls="static";text="title";left=295;top=19;right=373;bottom=39;transparent=1;z=3;};
static3={cls="static";text="imageName";left=295;top=62;right=371;bottom=82;transparent=1;z=5;};
static4={cls="static";text="ResKeys";left=16;top=182;right=76;bottom=218;transparent=1;z=8;};
static5={cls="static";text="welcome";left=295;top=103;right=371;bottom=123;transparent=1;z=14;};
tvLog={cls="static";left=295;top=370;right=823;bottom=431;transparent=1;z=13;};

)
/*}}*/

/**

V1.0：编辑功能
V1.1：添加功能

class Languages {
    var Language: String = ""
    var imageName: String = ""
    var title: String = ""
    var welcome = ""
}

var languages: ArrayList<Languages>? = null
var languageMap: HashMap<String, HashMap<String, String>>? = null
**/

var LanguagesCFG = {}
var keyLanguage = ""
var keyRes = ""
var filePath = ""
var resKeys = {}

mainForm.btnLoad.oncommand = function(id,event){
	filePath = fsys.dlg.open()
	reloadFile(filePath)
}

reloadFile = function(filePath){
	log("filePath", filePath)
	var content = string.load(filePath)
	LanguagesCFG = web.json.parse(content)
	log("languages", web.json.stringify(LanguagesCFG.languages))
	//var tableLanguage = {}
	mainForm.lbLanguages.clear()
	mainForm.lbResKey.clear()
	resKeys = {}
	keyLanguage = ""
	keyRes = ""
	mainForm.etLanguageTitle.text = ""
	mainForm.etLanguageWelcome.text = ""
	mainForm.etLanguageImageName.text = ""
	mainForm.etLangResContent.text = ""
	for (k,v in LanguagesCFG.languageMap) {
		//table.push(tableLanguage, k)
		mainForm.lbLanguages.add(k)
		
		for (kk,vv in v) {
			if (not table.find(resKeys, kk)) {
				table.push(resKeys, kk)
				mainForm.lbResKey.add(kk)
			}
		}
	}
}


log = function(tag, text) {
	//mainForm.tvLog.text = "[" + tag + "]: " + text
	//console.log(tag, text)
}

mainForm.lbLanguages.onSelChange = function() {
	keyLanguage = owner.selText
	resetContent()
	//log("key", key)
	var languages = LanguagesCFG.languages
	//log("languages", web.json.stringify(languages))
	for (i=1; #languages; 1) {
		var language = languages[i]
		//log("language", web.json.stringify(language))
		if (language.Language == keyLanguage) {
			mainForm.etLanguageTitle.text = language.title
			mainForm.etLanguageWelcome.text = language.welcome
			mainForm.etLanguageImageName.text = language.imageName
			return 
		}
	}
	mainForm.etLanguageTitle.text = ""
	mainForm.etLanguageWelcome.text = ""
	mainForm.etLanguageImageName.text = ""
}

resetContent = function() {
	log("keyLanguage", keyLanguage)
	var languageMap = LanguagesCFG.languageMap[keyLanguage]
	log("keyRes", keyRes)
	if (languageMap == null) {
		mainForm.etLangResContent.text = ""
		return 
	}
	var content = languageMap[keyRes]
	log("languageMap", web.json.stringify(languageMap))
	log("content", content)
	mainForm.etLangResContent.text = content
}


mainForm.lbResKey.onSelChange = function(){
	keyRes = owner.selText
	log("keyRes", keyRes)
	resetContent()
}

mainForm.btnLanguageSave.oncommand = function(id,event){
	//保存languages
	var languages = LanguagesCFG.languages
	if (languages == null) {
		msgboxErr("Please load Languages.CFG first.")
		return
	}
	
	var languageItem = {"Language":"", "imageName":"", "title":"", "welcome":""}
	languageItem.Language = keyLanguage
	languageItem.imageName = mainForm.etLanguageImageName.text
	languageItem.title = mainForm.etLanguageTitle.text
	languageItem.welcome = mainForm.etLanguageWelcome.text
	for (i=#languages; 1; -1) {
		var language = languages[i]
		if (language.Language == keyLanguage) {
			table.remove(languages, i)
		}
	}
	table.push(languages, languageItem)
	saveFile()
}

mainForm.btnLangResSave.oncommand = function(id,event){
	if (stringlen(keyLanguage) <= 0) {
		msgboxErr("Please select language first.")
		return
	}
	//保存languageMap
	var languageMap = LanguagesCFG.languageMap[keyLanguage]
	log("lastContent", languageMap[keyRes])
	languageMap[keyRes] = mainForm.etLangResContent.text
	log("currentContent", languageMap[keyRes])
	saveFile()
}

saveFile = function() {
	var file = filePath + ".tmp"
	string.save(file, web.json.stringify(LanguagesCFG))
	return file 
}

inputBox = function(title) {
	var input = win.inputBox()
	input.text = title
	input.info.text = ""
	input.btnCancel.text = "Cancel"
	input.btnOk.text = "OK"
	return input.doModal()
}

stringlen = function(text){
	if (text == null) {
		return 0
	}
	return string.len(text)  
}

msgboxErr = function(text){
	win.msgboxErr(text, "")
}

msgbox = function(text){
	win.msgbox(text, "")
}

mainForm.btnNewLanguage.oncommand = function(id,event){
	
	var languageMap = LanguagesCFG.languageMap
	log("languageMap", web.json.stringify(languageMap))
	if (languageMap == null) {
		msgboxErr("Please load Languages.CFG first.")
		return
	}
	
	var result = inputBox("Input " + mainForm.btnNewLanguage.text)
	log("inputBox", "stringlen : " + stringlen(result))
	if (stringlen(result) <= 0) {
		return
	}
	//检查重名
	for (k,v in languageMap) {
		if (k == result) {
			msgboxErr(result + " already exists.")
			return 
		}
	}
	
	languageMap[result] = {}	
	var filePath = saveFile()
	reloadFile(filePath)
}

mainForm.btnNewResKey.oncommand = function(id,event){
	if (stringlen(keyLanguage) <= 0) {
		msgboxErr("Please select language first.")
		return 
	}
	var result = inputBox("Input " + mainForm.btnNewResKey.text)
	if (stringlen(result) <= 0) {
		return
	}
	//检查重名
	if (table.find(resKeys, result)) {
		msgboxErr(result + " already exists.")
		return 
	}
	var languageMap = LanguagesCFG.languageMap[keyLanguage]
	keyRes = result
	languageMap[keyRes] = ""
	var filePath = saveFile()
	reloadFile(filePath)
}

mainForm.show();
return win.loopMessage();