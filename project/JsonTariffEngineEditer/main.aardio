import win.ui;
import console
import fsys.dlg
import classes
/*DSG{{*/
mainForm = win.form(text="TriStar Tariff Engine Editor V0.3";left=0;top=0;right=800;bottom=600)
mainForm.add(
btnAddRule={cls="button";text="Add Rule";left=166;top=490;right=246;bottom=527;z=11};
btnAddTariff={cls="button";text="Add Tariff";left=27;top=490;right=107;bottom=527;z=3};
btnCalculate={cls="button";text="Calculate";left=688;top=59;right=768;bottom=96;z=13};
btnEditbtnTariff={cls="button";text="Edit Tariff";left=27;top=536;right=107;bottom=573;z=12};
btnLoad={cls="button";text="Load Tariff File";left=272;top=538;right=547;bottom=575;z=1};
btnSave={cls="button";text="Save Tariff File";left=564;top=538;right=768;bottom=575;z=2};
etMaxAmount={cls="edit";left=280;top=73;right=425;bottom=96;edge=1;multiline=1;num=1;z=7};
etRule={cls="edit";left=456;top=149;right=768;bottom=474;edge=1;multiline=1;vscroll=1;z=10};
lbRule={cls="listbox";left=166;top=149;right=425;bottom=474;edge=1;items={};z=9};
lbTariff={cls="listbox";left=27;top=99;right=148;bottom=474;edge=1;items={};z=4};
static={cls="static";text="Tariff List";left=27;top=74;right=120;bottom=96;font=LOGFONT(h=-14;weight=700);transparent=1;z=5};
static2={cls="static";text="Rule List";left=166;top=119;right=259;bottom=141;font=LOGFONT(h=-14;weight=700);transparent=1;z=8};
static3={cls="static";text="Max Amount:";left=166;top=74;right=259;bottom=96;font=LOGFONT(h=-14;weight=700);transparent=1;z=6}
)
/*}}*/

stringifyJson = function(obj) {
	var objFormat = {}
	for (key, value in obj) {
		if (value == null) {
			continue
		}
		objFormat[key] = value
	}
	return web.json.stringify(objFormat)
}
formatJsonObject = function(jsonObject) {
	var json = stringifyJson(jsonObject)
	return formatJsonString(json)
}
formatJsonString = function(jsonStr) {
	var indentStr = "    "; // 定义缩进字符串，这里使用4个空格
    var newLine = '\r\n' // 定义换行符
    var result = ""; // 格式化后的字符串
    var arr = jsonStr // 将JSON字符串转换为字符数组

    var level = 0; // 记录当前的缩进层级

	for(i=1;#arr;1) {
        var char = string.charAt(arr, i)
        if (char == "{" || char == "[") {
            level ++
            result = result + char + newLine
            for (j=1;level;1) {
                result = result + indentStr
            }
        } elseif(char == "}" || char == "]") {
            level --
        	result = result + newLine
        	for (j=1;level;1) {
                result = result + indentStr
            }
            result = result + char
        } elseif(char == ",") {
            result = result + char + newLine
            for (j=1;level;1) {
                result = result + indentStr
            }
        } else {
        	result = result + char
        }
    }
    return result;
}

log = function(tag, text) {
	console.log(tag, text)
}
logJson = function(tag, jsonObject) {
	var json = formatJsonObject(jsonObject)
	log(tag, json)
}


msgbox = function(text){
	win.msgbox(text, "")
}

var filePath = ""
//var fileJson = {}
var listRuleSet = {}
var keyTariff = ""
var indexRule = 0

refresh = function() {
	mainForm.lbTariff.clear()
	for (k,v in listRuleSet) {
		mainForm.lbTariff.add(k)
	}
	initButtons()
	checkSave()
	checkEditbtnTariff()
}

reloadFile = function(filePath) {
	//log("filePath", filePath)
	if (filePath == null) {
		return
	} 
	var content = string.load(filePath)
	if (content == null) {
		return
	}
	listRuleSet = {}
	mainForm.lbRule.clear()
	mainForm.etMaxAmount.text = null
	mainForm.etRule.text = null
		
	var fileJson = web.json.parse(content)
	for (k in fileJson) {
		var ruleSet = fileJson[k]
    	listRuleSet[k] = ruleSet;
	}
	refresh()
}

refreshRules = function() {
	var rules = listRuleSet[keyTariff].rules
	
	//logJson("Rule.rules", rules)
	mainForm.lbRule.clear()
	for(i=1;#rules;1) {
		var rule = rules[i]
		//log("Rule.index", i)
		//logJson("Rule.rule", rule)
		mainForm.lbRule.add(i + ":" + rules[i].name)
	}
	checkEditbtnTariff()
}

mainForm.lbTariff.onSelChange = function() {
	keyTariff = owner.selText
	mainForm.etMaxAmount.text = listRuleSet[keyTariff].maxAmount
	refreshRules()
}

editRule = function(rule){
	..rule = rule
	var formFile = ""
	if (rule.type == "flat") {
		formFile = "\dlg\editRuleFlat.aardio"
	} elseif (rule.type == "free") {
		formFile = "\dlg\editRuleFree.aardio"
	} elseif (rule.type == "tier") {
		formFile = "\dlg\editRuleTier.aardio"
	} else {
		return 
	}
	
	var editRuleForm = win.loadForm(formFile)
	var result = editRuleForm.doModal()
	if (result) {
		var ruleResult = ..rule
		var json = formatJsonObject(ruleResult)
		ruleResult = web.json.parse(json)
		listRuleSet[keyTariff].rules[indexRule] = ruleResult
		mainForm.etRule.text = json
	}
}

mainForm.lbRule.onSelChange = function() {
	indexRule = tonumber(owner.selText)
	//log("Rule.index", indexRule)
	//log("Rule.keyTariff", keyTariff)
	var rules = listRuleSet[keyTariff].rules
	//logJson("Rule.rules", rules)
	var rule = rules[indexRule]
	//logJson("Rule.rule", rule)
	mainForm.etRule.text = formatJsonObject(rule)
	editRule(rule)
}

mainForm.btnLoad.oncommand = function(id,event){
	//加载*.trfjs文件
	filePath = fsys.dlg.open()
	//log("filePath", filePath)
	reloadFile(filePath)
}

saveFile = function() {
	if (filePath == null || filePath == "") {
		filePath = "tariff.json"
	}
	var file = filePath// + ".tmp"
	var json = formatJsonObject(listRuleSet)
	string.save(file, json)
	return file 
}
mainForm.btnSave.oncommand = function(id,event){
	filePath = saveFile()
	msgbox("file saved: " + filePath)
}

addOrEditTariffName = function(action) {
	if (action == "add") {
		..tariffName = null
	} elseif (action == "edit") {
		..tariffName = keyTariff
	}

	var formFile = "\dlg\editTariffName.aardio"
	var editTariffName = win.loadForm(formFile)
	var tariffName = editTariffName.doModal()
	if (tariffName == null) {
		return
	}
	if (action == "add") {
		keyTariff = tariffName
		listRuleSet[keyTariff] = {}
		refresh()
	} elseif (action == "edit") {
		var value = listRuleSet[keyTariff]
		listRuleSet[keyTariff] = null
		keyTariff = tariffName
		listRuleSet[tariffName] = value
		refresh()
	}
}

mainForm.btnAddTariff.oncommand = function(id,event){
	addOrEditTariffName("add")
}

mainForm.btnAddRule.oncommand = function(id,event){
	var formFile = "\dlg\editTypeName.aardio"
	var editTypeName = win.loadForm(formFile)
	var result = editTypeName.doModal()
	if (result) {
		var rule = ..rule
		var tariff = listRuleSet[keyTariff]
		if (tariff.rules == null) {
			tariff.rules = {}
		}
		table.push(tariff.rules, rule)
		refreshRules()
	}
}

mainForm.btnEditbtnTariff.oncommand = function(id,event){
	addOrEditTariffName("edit")
}

mainForm.btnCalculate.oncommand = function(id,event){
	..listRuleSet = listRuleSet
	var formFile = "\dlg\calculate.aardio"
	win.loadForm(formFile)
}

initButtons = function(){
	keyTariff = ""
	mainForm.btnAddTariff.hide = false
	mainForm.btnEditbtnTariff.hide = true
	mainForm.btnAddRule.hide = true
	mainForm.btnCalculate.hide = true
	mainForm.btnLoad.hide = false
	mainForm.btnSave.hide = true
}

checkSave = function(){
	var hide = string.len(table.tostring(listRuleSet)) <= 3
	mainForm.btnSave.hide = hide
	mainForm.btnCalculate.hide = hide
}

checkEditbtnTariff = function(){
	var hide = string.len(keyTariff) <= 0
	mainForm.btnEditbtnTariff.hide = hide
	mainForm.btnAddRule.hide = hide
}

initButtons()

mainForm.show();
return win.loopMessage();