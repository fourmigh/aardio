# web.layout( HTMLayout ) 界面开发入门

aardio 的扩展库 web.layout 基于 HTMLayout 组件。
而 aardio 扩展库 web.sciter 基于 Sciter JS 组件。

因为  Sciter JS 起源于 HTMLayout，基础的东西有很多相似的地方。
aardio 中的 web.layout 与 web.sciter  扩展库的基本用法是一样的，很多接口函数都是相同的。
web.layout 教程一般也适用 web.sciter，只要将示例代码中的 web.layout 简单替换为 web.sciter 即可。

但是 web.sciter（Sciter JS）支持 JavaScript，而 HTMLayout 只能在 CSS 中使用简单的 `CSSS!` 脚本。

HTMLayout 与 Sciter JS 最主要的区别是：
- web.sciter（Sciter JS）不支持 `CSSS!` 脚本。 
- web.layout( HTMLayout ) 不支持 JavaScript 脚本。

基础的 HTML,CSS 功能则 HTMLayout 与 Sciter JS 大同小异，HTMLayout 已经不再更新，而 Sciter JS 更新很快。

## 创建第一个 HTMLayout 窗口

在 aardio 开发环境新建 winform 窗体，点击【代码视图】按钮切换到代码视图，

找到显示窗体的代码 `winform.show();` 并在前面插入下面的代码：  

```
 import web.layout; //导入HTMLayout支持库  
 wbLayout = web.layout( winform ) //创建HTMLayout窗体  
 wbLayout.html = /**  
     在这里写入HTML  
 **/
```

经过我们改造后的完整的代码如下：

```
import win.ui; 
var winform = win.form(text="HTMLayout示例";right=599;bottom=399)
winform.add() 

// web.layout 这里很需要你  
import web.layout;  

/*  
winform窗体对象交给你，  
请帮我改造成可以显示网页的 HTMLayout 窗体，  
我准备给即将出生的 HTMLayout 窗体取个名字叫"wbLayout"  
并且使用 var 语句声明为局部变量，毕竟他还是个孩子，我可不想他满地乱跑变成大众情人  
*/  
var wbLayout = web.layout( winform )   
    
wbLayout.html = /**  
<div>
据说在 HTML 里这样就可以表示一个节点？  
</div>  
**/  
 
winform.show();   
win.loopMessage();  

```

当然，我们也不必要把 HTML，CSS 都写在一个文件里。  
可以创建一个aardio工程文件，css可以写到单独的 \*.css 文件里, html 可以写到单独的 \*.html 范例里，  
然后调用 wb.go( "/res/my.html" ) 打开 html 即可，html,css 都可以方便的加入内嵌资源生成独立绿色的 EXE 文件。  
  
注意HTML里面所有的节点都用一对放在尖括号里的标签表示，例如：

```
<标签名字> 内容 </标签名字>
```

节点里面又可以包含其他节点，例如：

```
<父节点标签名字> 
   <子节点标签名字> 内容 </子节点标签名字> 
</父节点标签名字>
```

就象一颗树一样，树根上面有很多树枝，而每个树枝上又可以长出很多的小树枝。  
HTML 使用的是树模型( 用个专业点的名词叫 DOM 模型，节点叫DOM节点 )。  

HTML 常用的标签就是 div ，div 是英文division（ 分区）的缩写，你可以理解为div就是一个盒子，  
大盒子里面可以有小盒子，小盒子里面可以有小小盒子，也就是 div 里面可以套 div。  


## HTMLayout 调试

请在 HTMLayot 示你代码中找到下面的代码  

```
import web.layout;
var wbLayout = web.layout( winform )
```

然后在后面输入 wbLayout.debug()  - 按回车键自动生成以下代码：  

```
import web.layout.debug;  
wbLayout.attachEventHandler( web.layout.debug );
```

上面的代码用于输出 HTMLayout 引擎内置的调试信息到控制台，  
并且添加一个全局函数 debug() 用于在 `CSSS!` 脚本中向控制台输出内容。  
添加了这两行代码以后，如果我们的 HTML,CSS 书写有错误会自动打开控制台并显示错误信息.  
  
## 使用 behavior 
 
在 aardio 中的  web.layout.behavior 名字空间下添加名字空间就可以创建 behavior。
也可以导入 web.layout.behavior 名字空间下面的库来创建 behavior。
 
HTMLayout 可以在 CSS 中指定 HTML 节点的 behaivor，
例如指定 `behaivor:"button.command"` ，这里的 behavior `"button.command"` 就对应于 aardio 中的 web.layout.behavior.button.command 名字空间。

CSS 的 behaivor 属性中的名字空间如果包含多级名字空间(也即包含圆点符号)则必须置于引号内部。

所有 HTMLayout 窗体可用的事件( 函数名前缀为 on....  ) 在 behavior 中都可以使用
当然, 仅在CSS样式中指定了 behavior 的节点才可以触发对应的事件。

示例：

```
import win.ui; 
winform = win.form(text="HTMLayout behavior用法演示";right=599;bottom=399) 

import web.layout;
wbLayout = web.layout(winform);
 
namespace web.layout.behavior.button.command {  
	
 	/*
 	ltOwner 参数是绑定behavior的节点,
 	实际上也就是指定了 behavior:command 的节点对象
 	
 	ltTarget 通常指的是实际触发事件的节点,
 	或者根据不同的事件,ltTarget的意义有所不同 
 	*/
 	onButtonClick = function (ltTarget,ltOwner,reason,behaviorParams) { 
		ltOwner.printf("点击了按钮");
		ltOwner.postEvent("onMyCustomEvent",2);//可以再次触发其他的事件,注意事件应为 web.layout.event.BEHAVIOR 名字空间下的事件或自定义事件
	}
	
	//自定义事件的参数与onApplicationEvent相同
	onMyCustomEvent = function (ltTarget,ltOwner,reason,behaviorParams) {
		ltOwner.printf("自定义事件onMyCustomEvent被触发,触发参数:%d",reason)
	}
	
	//CSS脚本中读取 self:value 时触发此回调，第2个返回值返回 value 的值
	onGetValue = function( ltOwner ){
		return true,"Value:onGetValue";
	}
	
	//CSS脚本中使用 self:value 修改值时触发此回调
	onSetValue = function(  ltOwner,value ){
		
		return true
	}

	//所有名字不是on前缀的函数,都可以在CSS脚本中直接调用,
	//在CSSS!调用下面的函数时,第一个实参是下面的第二个形参,第一个ltEle参数则是绑定behavior的节点对象
	func = function(ltOwner,a,b,c){  
		ltOwner.printf("调用了自定义函数,收到参数 a:%d b:%d c:%s ",a,b,c )
		return "返回新的值"
	}
}

wbLayout.html =/***
<div id="my-button" 属性="值">请点击这里</div> 
***/

wbLayout.css = /**
#my-button{ 
	/*
	behavior名字前加波浪线表示在原来的behavior列表上追加,而不是替换behavior属性。
	可以指定多个behavior,以空格分格,例如下面的 clickable 是一个内建behavior,表示触发 onButtonClick事件而不是 onMouseClick事件。
	*/
	behavior:"button.command clickable";
	active-on!:/*类似behavior中的onMouseDown,也即节点切换到active状态*/
		self.func(1,2,self:value), /*CSSS!脚本以逗号分隔语句*/
	; /*CSSS!脚本以分号表示语句块结束*//
}
**/
 
winform.show() 
win.loopMessage(); 
```
 
HTMLayout 有很多内建的 behavior，  
例如 button 节点这样的按钮就默认拥有内建 `behavior:button` 。

例如以下 CSS 代码：

```
#my-button{     
  behavior:command;      
}  
```    

如果给一个 button 节点指定自定义 behavior 时，就覆盖并清除了默认拥有的 behavior。  
也就是说他虽然有 button 的默认CSS样式外观，却在行为上不再是一个 button了，当然鼠标点击的时候也就不再触发 onButtonClick 事件，而是象普通的 div 等节点一样触发 onMouseClick 。  
  
我们也可以添加 behavior，就是给节点添加新的 behavior 但是又不覆盖节点已经拥有的 behavior.  
在 HTMLayout 中使用波浪号表示添加 behavior，例如：  `behavior:~command;` 波浪号在名字前面表示追加 behavior，波浪号在名字后面表示插入behavior，你可以把 ~ 理解为原来的 behavior 列表。  
  
如果要同时为节点指定多个 behavior，则可以使用空格分格，例如： 

例如以下 CSS 代码：

```
#my-button{    
  behavior:button command;  
    
}  
```  
 
除了象 button 这样 HTMLayout 引擎内建的 behavior，以及我们自定义的 behavior，  
aardio标准库同样提供了很多behavior。  
  
使用标准库的 behavior 只要导入指定的名字空间即可，  
例如模拟窗口标题栏按钮的 web.layout.behavior.windowCommand;  

# CSSS! 脚本

## 入门概述

CSSS! 是在标准CSS语法基础上扩展的一种简单脚本语言，以实现一些简单的交互行为。
CSSS! 基于标准CSS语法，通常是以一个属性名称（该名称以惊叹号结束)标明触发的事件，以逗号分隔语句（可不是一般编程语言中使用的分号哦），而以分号结束语句块（不是大括号哦），这些非常规的规则为是了遵守CSS语法规范。 

让我们看看下面这段 CSSS! 脚本:

```css
.item {
      hover-on! :
            ele = $1( input.url ) ,
            ele:empty == true ?
                  (self.value = "empty") #
                  (self.value = "filled"),
            ele:hover = true,
            self::width = ele.box-content-width(),
            self.$(.icon) -> @(ele) ele::background-color = rgb(255,0,0)
      ;
}
```

它（`CSSS!`）的格式看起来就像是个扩展的 CSS 属性一样.

## CSSS! 脚本语法规则

### 基本规则

- CSSS! 语句使用逗号","作为语句结束符.
- CSSS! 使用分号结束语法块。
- CSSS! 中的字符串只能双引号标识, 不能使用单引号("string").
- CSSS! 中使用关键字 self 表示当前对象.

`CSSS!` 嵌入标准 CSS 并遵守CSS语法规则,受限于 CSS 语法，脚本并不使用分号分隔语句，也不会使用大括号表示语句块，而是使用逗号分隔语句，使用分号结束语法块。 
  
### 标志识

标志符可以使用英文字母, `-`,  `_` 或者 `$`符号,在这些后面可以附加数字，

注意CSS允许横线作为标志符的一部分，这与其他编程语言不同，如果你需要用横线表示操作符，例如负号，那么清添加空格以区别。

  
### 关键字

CSSS! 支持下面的关键字

`true`   `false`   `null`   `self` 
    

true 表示逻辑真，false 表示逻辑否，null 表示空值，  
  
而 self 表示自身，这里的 self 可不是 aardio 中表示当前名字空间的 self 对象，  
实际上他更像 aardio 中表示自身的 this 或者 owner 对象。在 CSSS! 脚本中，  
self 指当前触发事件的节点（类似 javascript 中的 this 对象)  
  
### 操作符

`<`   `>`     `<=`    `==`    `!=`    `>=`    `&&`    `||+`     `-`     `\*`     `/`     `%`     `^`     `|`     `&`

### 三元操作符

这个语法的作用类似条件判断语句,

也有点像 aardio 中的三元操作符（区别是在CSSS!中使用 `#` 号，而不是类似 aardio 或 C++ 中的冒号，当然原因也很简单，冒号被 CSS 语法占用了 )

CSSS! 示例：

`self:value > 12 ?( self:current = true, self.scroll-to-view()); `

上面这句代码是指，如果当前节点的值大于12，那么选中当前节点( 改变current状态)，并且滚动到视图范围。  
  
### 数据类型  
  
CSSS! 变量用到的类型如下：  
  
null - 空值;  
boolean 逻辑值，两个可选值 true 或 false;  
integer - 32位整数值;  
float - 64bit浮点值(实际上这里指的是aardio或C++中的double类型);  
string - UNICODE字符串;  
object - DOM节点对象(引用);  
object-set - DOM节点对象的集合，函数 $() 返回这样的类型.  
function-reference - CSSS!中定义的函数  
  
  
###  字面值  
  
支持整数，浮点小数，文本, 长度单位，以及正则表达式等字面量 :

1.  整数格式 -> \[0-9\]+ | '0x' \[0-9A-Fa-f\]+ | ''' character ''' | key-code-literal  
    
2.  浮点数格式 -> \[0-9\]+ '.' \[0-9\]+浮点数格式 -> \[0-9\]+ '.' 'e'|'E' '+'|'-' \[0-9\]+  
    
3.  文本字面量 -> '"' \[.\]\* '"'  
    
4.  正则字面量 -> '/' <single line regular expression> \['/ig'\] ';'  
    
5.  长度字面量 -> <integer-literal> | <float-literal> immediately followed by one of: 'pt' | 'px' | 'pc' | '%' | 'em' | 'ex' | 'in' | 'cm' | 'mm'

数值支持0x前缀表示的十六进制数值，也可以在数值前使用正负号。  
而文本需要放在双引号中。正则字面量则与javascrip;类似，至于长度大小等可以使用px(像素) pt(点)等单位，与CSS相同。  
  

### 键盘代码字面量
  
键盘代码放在单引号中，可以是单个英文字母，也可以是下面的值：

'RETURN' | 'LEFT' | 'RIGHT' | 'UP' | 'DOWN' | 'PRIOR' | 'NEXT' | 'TAB' | 'HOME' | 'END' | 'DELETE' | 'INSERT' | 'BACK'


如果按下组合键，例如 Ctrl+X \\则在键名前加上 '^'.

字面量使用示范:

34 - 表示普通整数     
    
0xAFED1234 - 表示十六进制整数           
    
1.52 - 表示浮点数           
    
1.e2 - 表示浮点数           
    
1.e-2 - 浮点数"Hello world!" - 字符串           
    
12pt - 表示12象素.           
    
'A' - 表示A的键盘代码，实际是一个数值


使用键盘代码的示例：  

```css
key-on!:  key-code() == '^A'? ... # // Ctrl+A 组合键    
      key-code() == '^NEXT'? ... ; // Ctrl+PgDown 组合键
```
  
### 注释语法

  
注释语句被忽略不会被编译，类似aardio的注释语法有两种格式:  
  
单行注释以斜杠开头:

'//' <text-no-crlf> end-of-line


多行注释语法也类似aardio:

'/\*' <text> '\*/'

  
### 语句

  
CSSS! 脚本使用逗号 ','分隔语句，

语句块以分号 ';'.结束 ，下面是支持的语法规则:

  
1.  赋值语句:

<左值> '=' <表达式>.


2. 变量声明:

`<变量名> <b>'='</b> <表达式>`

3. 语句块

语句块是一系列使用逗号分隔的语句，语法规则如下：

`'('  <expression> [, <expression>]*  ')'`
  
4. 条件语句

  
语法规则:

`<logical-expresion> '?' <when-true-expression> [ '#'  <when-false-expression> ]`


CSSS! 的条件判断语句格式很简单,  
类似 Javascript或aardio中 的 三元操作符 …  ?  …  :   …  组合，区别是用#号代替了冒号:

`判断条件 ? (条件为真时的操作)`

或者:

`判断条件 ? (条件为真时的操作)`   

5. 循环

不支持循环，但是函数可以支持尾递归调用.

6. Return语句

return 语句用在函数中返回一个值:

return <expression-or-value-to-return>

7. For each, 枚举语句  
  
语法规则如下：

<object-set> '->'  <reference-to-function-with-single-parameter>



枚举通常被用于处理元素集合,格式如下:

    集合 -> @(参数名) 操作语句

aardio中类似的遍历集合的代码如下：      

    for 局部变量名 in 集合{ }


范例:

```css
  input.number {
       value-changed! :
         total = 0,
         $(input.number) -> @(el) total = total + el:value,
         $1(td#total):value = total;
     }
```

上面代码的解释: 设页面上有一个HTML节点 `<input class="number">` 在它的值发生改变量触发下面的操作:  

- 定义一个变量 total 并且初始化他的值为 0,  
- 定义一个函数 `@(el) total = total + el:value`,  
- 枚举所有匹配CSS选择器 `input.number` 的节点，并执行`->`操作符后面指定的函数,  
- 将 total 的值赋于第一个匹配CSS选择器 `td#total`的节点  
  
  
### 访问DOM节点的属性、状态值、CSS样式

  
1.  可以使用成员操作符圆点访问节点的属性:

例如对于当前节点: <input type="text"> 有下面的脚本代码:

t = self.type


将会获取节点的type属性并赋值给变量 t.  

  
2.  节点的状态使用单个冒号 `:` 作为成员操作符:

支持的CSS状态例如:  `:hover`, `:active`, `:link`,  `:checked`, `:current` 等等。


在 CSSS! 中有如下状态可在执行时使用:


| **状态**<br><br> | **说明**<br><br> |
| --- | --- |
| ele:value<br><br> | 元素DOM节点对象的值( **注意在CSSS!中 value 是作为一个状态值来访问，不是属性值，是以不能写为 ele.value, 而应当改用冒号写为 ele:value** ), 对于输入框这个值为输入的文本. 其他元素为内部的文本.<br><br> |
| ele:index<br><br> | 元素在子元素的序号. 取值从 1 开始到 self.parent().children()<br><br> |
| ele:hover<br>ele:active<br>ele:empty<br>ele:readonly<br>ele:disabled<br>ele:focusable<br><br> | 部分CSSS!中常用的布尔型状态. (true/false)<br><br> |


3.  CSS 属性使用两个冒号 `::` 作为成员操作符 :  
  
下面是一个范例，在定时器中逐渐的将对象改变为透明状态。

```
self::opacity = self::opacity + 0.1 #  
return cancel;  
```

### Self对象
  
在 CSSS! 脚本事件中表示当前节点  


### Cancel

  
这个关键字是专用于return语句，以终止事件继续传递r.

  
### 函数


1. 定义函数


对于一般的函数, 定义的格式如下:

```css
foo = @( a, b ) c = a + b, return c  
```
    
 
aardio 中类似的定义函数的代码如下：

```aardio
foo = function( a, b ) { 
    c = a + b; return c; 
} 
```

CSSS! 也支持匿名函数定义。

2. 调用函数

  
调用函数比较简单,下面的脚本调用函数foo()，传入参数1,2，并将返回值赋值给变量bar:

```css
bar = foo(1,2) 
```  

## 常用函数

### 在 CSSS! 中获取 HTML 页面元素的所有函数如下:

| CSSS!函数<br><br> | 函数说明<br><br> |
| --- | --- |
| $1(.item)<br><br> | 获取匹配 ".item" 的第一个元素<br>类似aardio中的wbLayout.queryEle()函数(唯一的区别是在aardio中需要将CSS选择器参数放在引号中 - 即使用字符串包含CSS选择器)<br><br> |
| $(.item)<br><br> | 获取所有匹配 ".item" 的元素.<br>类似aardio中的wbLayout.queryEles()函数(在aardio中需要将CSS选择器参数放在引号中 - 即使用字符串包含CSS选择器)<br><br>$(.item) 返回的集合可以象单个元素一样直接使用,例如:<br>$(.item).属性 = 值, <br><br>返回值也可以用于枚举, 例如: $(.item)->ele(ele) ele.属性 = 值;<br><br><br><br><br> |
| ele.$1(.item)<br><br> | 获取ele子元素中匹配".item"的第一个元素<br><br> |
| ele.$(.item)<br><br> | 获取ele子元素中匹配".item"的所有元素<br>类似aardio中的ele.queryElements()函数<br><br> |
| ele.$1p(.item)<br><br> | 获取父元素中匹配".item"的最近的一个元素.<br>类似aardio中的ele.queryParent()函数<br><br> |
| ele.$p(.item)<br><br> | 获取父元素中所有匹配".item"的元素.<br><br> |
| ele.parent()<br><br> | 获取父元素<br><br> |
| ele.next()<br><br> | 获取同级的下一个元素<br><br> |
| ele.previous()<br><br> | 获取同级的上一个元素<br><br> |
| ele.child(3)<br>ele.children()<br><br> | 获取第3个子元素. 参数范围 1 … ele.children().<br>可以通过 ele.children() 获取子元素的数目.<br><br> |

  
CSS选择器参数中可以使用变量(包含在尖括号中), 示例:

```css
ncol = 2, $( table td:nth-child(< ncol >)).some-attr = "hi!";
```

上面的CSSS!脚本对所有符合CSS选择器 `table td:nth-child(2)` 规则的节点改变属性  `some-attr`  的值为  `"hi!" ` 
  
### 其他全局函数

*   int(val)- 转换一个值为整数r;
*   float(val)- 转换一个值为浮点数，类似aardio中的tonumber;
*   length(val) - 转换一个值为长度值（包含单位);
*   min(val1, val2 ... valN)- 返回最小数;
*   max(val1, val2 ... valN)-返回最大数;
*   limit(val, minval, maxval) - 返回一个值是并限定允许的最小或最大值  
    
### String 字符串对象的一些成员函数

  
*   string.length - 取字符串长度
*   string.toUpper() -  将字符串转为大写;
*   string.toLower() -  将字符串转为小写
*   string.slicestr(start\[, length = -1\]) -  截取字符串，参数一指定开始位置，参数二指定长度，如果小于零，则截取到尾部。  
    

### HTML节点对象提供的一些内置函数  
  
ele.children() - 返回子节点数目  
ele.child(n:integer) - 返回指定子节点,参数为索引  
ele.next() -返回下一个兄弟节点  
ele.previous() -返回上一个兄北节点  
ele.parent() - 返回父节点  
ele.text-width("string") - 计算当前节点在当前样式下显示指定文本所需的像素宽度.  
ele.min-intrinsic-width(), ele.max-intrinsic-width() - 节点最小,最大内容宽度  
ele.min-intrinsic-height(), ele.max-intrinsic-height() - 节点最小,最大内容高度  
ele.system-scrollbar-width(),ele.system-scrollbar-height() - 系统设定的滚动条宽度,高度  
ele.system-border-width() - 系统设定的边框宽度  
ele.system-small-icon-width(),ele.system-small-icon-height() - 系统设定的小图标宽度,高度  
ele.foreground-image-width(),ele.foreground-image-height() - 当前节点的前景图片高度,高度  
ele.background-image-width(),ele.background-image-height() - 当前节点的背景图片高度,宽度  
  
ele.box-type-what() 返回节点位置大小等相关参数,  

其中type必须被替换为以下名字之一：

    - margin - margin box (节点包含外边距空间)  
    - border - border box(节点包含边框空间);  
    - padding - padding box (节点包含内边距空间);  
    - content - content box (节点内容空间).  
    - inner - inner box(节点内部空间);  
    - what part of the name is one of:  
   
 what必须被替换为以下名字之一：

    - left - 左边距;  
    - right - 右边距;  
    - top - 顶边距  
    - bottom - 底边距;  
    - width - 宽度;  
    - height - 高度;  
   
例如 `self.box-border-width()` 返回包含节点包含边宽的宽度。

ele.x-parent() 获取相对父节点的x坐标;  
ele.x-root() 获取相对HTML根节点的x坐标  
ele.x-view() 获取相对当前窗口的x坐标  
ele.x-screen() 获取相对屏幕的x坐标  
  
ele.y-parent() 获取相对父节点的y坐标;  
ele.y-root() 获取相对HTML根节点的y坐标  
ele.y-view() 获取相对当前窗口的y坐标  
ele.y-screen() 获取相对屏幕的y坐标  
  
ele.scroll-to-view()  滚动节点到当前视图  
  
ele.start-animation(duration)  
开始执行动画,该函数触发 animation-start! animation-step! animation-end!等事件，　  
duration 参数指定动画持续时间， 例如： self.start-animation(0.4s) 启动动画 - 持续时间为400ms(毫秒)  
  
ele.stop-animation()  
停止执行动画。最好不要同时启动多个动画效果，停止当前动画再启动效的动画。  
  
ele.start-timer(period:integer)  
启动定时器，参数指定时间间隔，以毫秒为单位，该函数触发  timer! 事件，在该事件中调用return cancel取消定时器，  
也可以调用  stop-timer() 函数取消定时器。  
  
ele.stop-timer()  
取消定时器  
  
注意CSSS!脚本中使用横线作为标志符的一部分是合法的,  
这在其他编程语言,以及aardio里是行不通的. 请注意区分!  

## CSSS! 触发事件(Active CSS attributes)

在前面的示例中，hover-on! 是我们的脚本要处理的事件标识.
当具有 .item 类的元素被鼠标悬停时, 会触发此事件并执行里面的代码.

下面是完整的事件支持列表:

- `hover-on!`

    鼠标悬停在节点上触发，类似aardio中的  wbLayout.onMouseEnter 事件

- `hover-off!`

    鼠标离开节点时触发，类似aardio中的   wbLayout. onMouseLeave 事件

- `active-on!`

    鼠标按下时触发，类似aardio中的   wbLayout.onMouseDown 事件

- `active-off!`

    鼠标抬起时触发, 类似aardio中的 wbLayout.onMouseUp 事件

- `focus-on!`
    
    获得输入焦点时触发,

- `focus-off!`

    失去输入焦点时触发,

- `key-on!`，`key-off!`

    键盘的按键按下/抬起时触发，通过 key-code() 函数获取按键信息.
    key-code() 获得的按键信息可能是一个用单引号包含的有效字符('a', '4', '$')或是下列预定义值之一:
    'RETURN', 'LEFT', 'RIGHT', 'UP', 'DOWN', 'PRIOR', 'NEXT', 'TAB', 'HOME', 'END', 'DELETE', 'INSERT', 'BACK'

- `click!`，`double-click!`

    鼠标单击，或双击时触发，必须绑定了所有允许接收单击，或双击事件的behavior，储如 button , hyperlink 等等。其他节点一般不会触发这两个事件（这些节点也可通过在CSS中指定 behavior:clickable; 启用该事件） 。

- `animation-start!`，`animation-step!`，`animation-end!`

    动画控制事件animation-start! 在调用了元素的 element.start-animation() 方法后触发.animation-step! 事件处理的最后必须返回一个整数(下次执行的间隔毫秒数). 例如: return 500;

- `timer!`

    定时触发器，配合 start-timer(ms) 和 stop-timer() 函数使用.start-timer(ms) 中的参数单位为毫秒.

- `size-changed!`

    元素大小发生改变时触发

- `value-changed!`

    input 类控件的值发生变时时触发

- `assigned!` 

    这个属性很有意思，就是在你在 CSS 给 `assigned!`  这个属性赋值时触发，
    换句话说，其实就是你写在这后面的脚本作为CSS属性被应用到节点时触发，而不是等待用户的什么交互。这个事件的作用类似于在behavior 里的 onAttach 事件。

## 在 aardio 中使用 `CSSS!` 脚本示例

aardio 调用 HTMLayout 并使用  `CSSS!` 脚本的范例程序：

```aardio
import win.ui;
var winform = ..win.form( text="CSSS! 脚本示例" )
winform.add()

import web.layout; //首先导入HTMLayout支持库
var wbLayout = web.layout(winform); //创建HTMLayout窗体

wbLayout.html = /*********
<html>
<head>
  <style>

  //下面的选择符表示拥有 autofocus属性的节点，并且状态为可以获取焦点(focusable)
  [autofocus]:focusable {
    assigned!: self:focus = true; //assigned!事件表示加载脚本即时执行
  }
  </style>  
<head>
<body>
  在页面加载以后，第二个文本框自动得到焦点:<br/>
  <input type="text" value="测试文本框" /><br/>
  <input type="text" value="看我自动得到焦点了" autofocus />
</body>
</html>
*********/

winform.show()
win.loopMessage();
```

## 在 aardio 中使用 HTMLayout 与 `CSSS!` 事件的范例

aardio 代码如下：

```aardio
import win.ui; 
var winform = ..win.form( text="HTMLayout CSSS!脚本事件触发例子" )  

import web.layout;
var wbLayout = web.layout(winform);

wbLayout.html = /*********
<html>
  <head>
    <style>
      p#test {
        border:1px solid black; //定义边框
        width: 30%; //定义宽度
        hover-on!: self.start-timer(400); //鼠标悬停时,开始定时器,间隔400毫秒
        hover-off!: self.stop-timer(),
                    $1(popup#for-test):popup = false ; //注意冒号作为成员操作符时改变节点的状态
                    
         //注意下面用到了条件语句,问号前面是条件
        timer!: self:hover ? self.show-popup($1(popup#for-test),3,9), /*参数表示在右下角弹出提示 */
                return cancel;//阻止事件继续传递
      }
      
      popup#for-test  {
        margin-top:10px; /*指定顶边距 */
      }
   
    </style>
  </head>
<body>
  自定义提示
  <p #test>请将鼠标放在这里</p>
  
  <popup #for-test>
    哈哈,你成功了
  </popup>

</body>
</html>

*********/

winform.show()
win.loopMessage();
```


# 在 aardio 中调用 HTMLayout 与 CSSS! 脚本的入门示例

我们下面看一个简单的aardio范例

假设有一个 HTML 节点内容是这样：

```html
<label for="id">
```

那么我们可能如下编写 CSS，下面的 CSS 选择器 `[for]` 匹配所有指定了 for 属性的节点，在 CSS 选择器中，中括号内问指定 HTML 属性。

```css
   [for] {
     hover-on!  : $1(#< self.for >):hover = true;
     hover-off! : $1(#< self.for >):hover = false;
     active-on! : $1(#< self.for >):focus = true;
     cursor:pointer;
   }
   input:hover {
     outline: 4px glow blue 1px;
   }
```

鼠标悬停或离开节点导致节点for属性绑定的节点同步改变 `:hover` 状态，而点击 lable 节点，`for` 属性绑定的节点将获得输入焦点，下面我们看一下 aardio 实现的完整范例：

```aardio
import win.ui; 
var winform = win.form( bottom=399;parent=...;text="aardio Form";right=599 )

import web.layout;
wbLayout = web.layout(winform)

wbLayout.html = /**
<input type="text" />

<input type="text" id="myText" value="" />
<label for="myText" >鼠标放到这里，并点击这里</lable>
**/


wbLayout.css = /**
[for]
   {
     hover-on!: $1(#< self.for >):hover = true;
     hover-off!: $1(#< self.for >):hover = false;
     active-on!: $1(#< self.for >):focus = true ;
     cursor:pointer;
   }
   input:hover {
     outline: 4px glow blue 1px;
   }  
**/

winform.show();
win.loopMessage();
```

# 在 aardio 中调用 HTMLayout 并使用 CSS 扩展的示例

HTMLayout 自己扩展了一些 CSS 语法，以下是使用示例：

```aardio
import win.ui;
/*DSG{{*/
var winform = ..win.form( bottom=399;parent=...;right=599;text="aardio Form" )
winform.add(  )
/*}}*/

import web.layout;
var wbLayout = web.layout(winform);

wbLayout.html = /*********
<html>
<head>
  <style>
    div.slave { visibility:collapse; } //指定了slave类的节点默认折叠
   
    //注意下面的加号,这是一个关系选择符,表示加号前面的节点后面的第一个节点
    widget.master:checked + div.slave { visibility:visible; }
  </style>  
<head>
<body>

  <widget type="checkbox" .master>请点选这里:</widget>
  <div .slave>
    <li>item #1</li>
    <li>item #2</li>
    <li>item #3</li>
  </div>
  
  <widget type="checkbox" .master>请点选这里试试:</widget>
  <div .slave>
    <li>item #1</li>
    <li>item #2</li>
    <li>item #3</li>
  </div>
</body>
</html>
*********/

winform.show()
win.loopMessage();
```
 


 

