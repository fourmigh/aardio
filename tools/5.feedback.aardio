//反馈  
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio 快速反馈工具";right=777;bottom=615)
winform.add(
bk={cls="bk";left=100;top=79;right=433;bottom=171;bgcolor=16777215;border=1;dl=1;dr=1;dt=1;z=2};
btnReport={cls="button";text="提交反馈";left=442;top=131;right=607;bottom=171;color=14120960;dr=1;dt=1;font=LOGFONT(h=-14);z=13};
chkFixAi={cls="checkbox";text="复制 AI 对话自动输入反馈内容(可自动修复语法错误)：";left=100;top=179;right=564;bottom=204;checked=1;dl=1;dr=1;dt=1;z=18};
edit={cls="edit";text="注册:用户名@密码";left=582;top=58;right=727;bottom=84;dr=1;dt=1;edge=1;z=10};
editChatUrl={cls="edit";left=100;top=141;right=433;bottom=171;dl=1;dr=1;dt=1;edge=1;multiline=1;z=7};
editMessage={cls="edit";left=100;top=206;right=752;bottom=574;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=14};
editPwd={cls="edit";left=100;top=48;right=433;bottom=78;dl=1;dr=1;dt=1;edge=1;multiline=1;password=1;z=6};
editUser={cls="edit";left=100;top=13;right=433;bottom=43;dl=1;dr=1;dt=1;edge=1;multiline=1;z=3};
lnkHelp={cls="plus";text="《 提问前必读：解决编程问题的 10 点建议 》";left=94;top=583;right=386;bottom=607;align="left";color=8388608;db=1;dl=1;dr=1;font=LOGFONT(h=-13);notify=1;textPadding={left=5};z=16};
lnkQuery={cls="plus";text="查询回复消息";left=434;top=582;right=726;bottom=606;align="left";color=7348256;db=1;dr=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(name='FontAwesome')};iconText='\uF003 ';notify=1;textPadding={left=17};z=19};
plus={cls="plus";left=467;top=27;right=560;bottom=110;bgcolor=16777215;dr=1;dt=1;foreRepeat="scale";foreground="\.res\wx.jpg";repeat="scale";z=9};
plus2={cls="plus";left=442;top=13;right=752;bottom=124;bgcolor=16777215;border={radius=7};dr=1;dt=1;z=1};
plusShare={cls="plus";left=110;top=79;right=414;bottom=140;dl=1;dr=1;dt=1;foreRepeat="scale";foreground="\.res\share.gif";z=17};
static={cls="static";text="用户名：";left=7;top=17;right=98;bottom=42;align="right";dl=1;dt=1;transparent=1;z=4};
static2={cls="static";text="密码：";left=7;top=56;right=98;bottom=77;align="right";dl=1;dt=1;transparent=1;z=5};
static3={cls="static";text="AI 对话链接：";left=16;top=150;right=98;bottom=171;align="right";dl=1;dt=1;transparent=1;z=8};
static5={cls="static";text="微信公众号发送：";left=582;top=30;right=698;bottom=54;dr=1;dt=1;transparent=1;z=11};
static6={cls="static";text="注册账号";left=582;top=88;right=640;bottom=117;dr=1;dt=1;transparent=1;z=12};
static7={cls="static";text="反馈内容：";left=4;top=205;right=98;bottom=230;align="right";dl=1;dt=1;transparent=1;z=15}
)
/*}}*/

import win.clip;
import win.clip.chromiumUrl;
import win.clip.viewer; 
var clipViewer = win.clip.viewer(winform);

import ide;
import win.version;
import ide.aifix;
import inet.url;
clipViewer.onDrawClipboard = function(){
	var text = win.clip.read();
	if(!#text) return;
	
	var url = string.match(text,"https\://chatglm\.cn/share/\S+");
	if(!url) url = string.match(text,"https\://www\.coze\.cn/s/\S+");
	if(!url) url = string.match(text,"https\://yuanqi\.tencent\.com/share/\S+");
	if(!url) url = string.match(text,"https\://\w+\.com/s/\S+");
	
	if(url){
		winform.editChatUrl.text = url;
		return;
	}
	
	if(!winform.chkFixAi.checked){
		return;
	}
	
	if(#winform.editMessage.text
		&& winform.editMessage.modified){ 
		return;
	}

	var chromiumUrl = win.clip.chromiumUrl.read();
	if(!#chromiumUrl){
		return;//不是在网页上复制的内容
	}   
	
	
	var text,count = string.replace(text,"```\s+aardio(.+?)\s+```",function(c){
		return ide.aifix(c);
	})
	
	
	if(count){ 
		winform.editMessage.text = text;
		winform.editMessage.modified = false;
	}
	else{ 
		if(#text / string.len(text) > 1.65){
			return;
		}  
		
		var text2 = ide.aifix(text);
		if(text2!=text ||
			string.find(text,"import\s+console")
			|| string.find(text,"import\s+win.ui")	
			|| string.find(text,"console.log")	
			|| string.find(text,"win.form")	
			|| string.find(text,"console.pause")	
			|| string.find(text,"winform.show")	
			|| loadcode(text2)
		){
			winform.editMessage.text =  text2;
			winform.editMessage.modified = false;
		}
	}  
}


clipViewer.onDrawClipboard();

import fsys.config;
var config = fsys.config(io.appData("aardio/std/report"));
winform.bindConfig( config.winform,{
	edit = {
		"editUser":"text"
	} 
} );

import ide;
import win.version;
var ideVersion = ide.getConfig("version");

import crypt;
import web.rest.jsonLiteClient;
var http = web.rest.jsonLiteClient("aardio/v" + _AARDIO_VERSION + "(IDE "  + ideVersion + "," + win.version.name + ")");
		
http.beforeStringifyRequestParameters = function(params,codepage){
	if(params.pwd) params.pwd = crypt.sha256(params.pwd);
	return params; 
}
var report = http.api("http://api.aardio.com/api/report/{...}/");

if(!#winform.editMessage.text){
winform.editMessage.text = '- ' + win.version.format() 
	+ '\r\n- aardio v' + ide.getConfig("version")
	+ '\r\n\r\n# 重现问题步骤如下\r\n\r\n- \r\n- \r\n\r\n# 重现问题代码如下\r\n\r\n';	
}

winform.editMessage.setFocus();

winform.editMessage.limit = 204800 - 255;
winform.editChatUrl.limit = 255;
	
winform.btnReport.oncommand = function(id,event){
	var url = winform.editChatUrl.text;
	var user = winform.editUser.text;
	var	pwd = winform.editPwd.text;
	
	if(!#user){
		return winform.editUser.showErrorTip("错误 - 未指定用户名","请指定用户名。使用微信注册用户名以后，才能在微信公众号查询处理结果");
	}
	
	if(!#pwd){
		return winform.editPwd.showErrorTip("错误 - 未指定密码","请指定用户密码。"); 
	}
	
	if(!#url){
		return winform.editChatUrl.showErrorTip("错误 - 未指定分享链接","请在 AI 会话页面点击复制按钮。");
	}
	
	var urlText = url;
	var url = string.match(urlText,"https\://chatglm\.cn/share/\S+");
	if(!url) url = string.match(urlText,"https\://www\.coze\.cn/s/\S+");
	if(!url) url = string.match(urlText,"https\://yuanqi\.tencent\.com/share/\S+");
	if(!url) url = string.match(urlText,"https\://\w+\.com/s/\S+");
	
	if(!#url){
		return winform.editChatUrl.showErrorTip("错误 - 无效的分享链接","请在 AI 会话页面点击复制按钮。");
	}
	
	var msg = winform.editChatUrl.text + '\r\n' + winform.editMessage.text;
	
	if(string.len(msg)>204800){
		return winform.msgboxErr("反馈信息总字数不能超过 200 KB !")
	}
	
	if(!#winform.editMessage.text){
		return winform.editMessage.showWarningTip("反馈内容不能为空");
	}
	
	var result = report.post({
		user = winform.editUser.text;
		pwd = pwd;
		url = msg;
	})	
	
	if(result.message){
		winform.msgbox(result.message)
	}
	else {
		winform.msgbox(result.error)
	}
	
}

winform.editMessage.onChange = function(){ 

	if(#owner.text){
		if(!winform.editMessage.modified){
			return;
		}
		
		winform.chkFixAi.checked = false;
		winform.chkFixAi.disabled = true;		
	}
	else {
		winform.chkFixAi.checked = true;
		winform.chkFixAi.disabled = false;
		winform.editMessage.modified = false;	
	} 
}

////参数也可以是返回菜单项数组的函数
import win.clip.bitmap;
winform.editMessage.enablePopMenu(
	function(){
		var canPaste = win.clip.bitmap.test();
		
		return { 
			{ /*---分隔线---*/ };  
			
			{ "插入剪贴板图像";
				function(id){
					
					var md = win.clip.bitmap.readMarkdown();
					if(md){
						winform.editMessage.selText = md;
					} 
					
				} ; lambda()!canPaste ? 1/*_MF_GRAYED*/ : 0
			};
			
				{ "一级标题";  function(id){
					winform.editMessage.selText = '\r\n# 标题\r\n\r\n'
				} };
				{ "二级标题";  function(id){
					winform.editMessage.selText = '\r\n## 标题\r\n\r\n'
				} };  

				{ "无序列表";  function(id){
					winform.editMessage.selText = '
\r\n
* \r\n
* \r\n
* \r\n
\r\n
'
				} };
				{ "有序列表";  function(id){
					winform.editMessage.selText = '
\r\n
1. \r\n
2. \r\n
3. \r\n
\r\n
'
				} };
				{ "引用块";  function(id){
					winform.editMessage.selText = '\r\n>'
				} };
				{ "代码块";  function(id){
					winform.editMessage.selText = '\r\n```\r\n\r\n```'
					winform.editMessage.setsel(winform.editMessage.getsel() - 5)
				} };
		}
	}
)

winform.lnkHelp.oncommand = function(id,event){
	import process;
	process.openUrl("https://mp.weixin.qq.com/s?__biz=MzA3Njc1MDU0OQ==&mid=2650933548&idx=1&sn=1d500b49c6de73f72bf884dd3e01d7cb&chksm=84aa2296b3ddab80cf8393283a884694b6464929c7f890edf26aadaa4738c622229baa4dbbe5&token=629236371&lang=zh_CN#rd")
}
 
winform.lnkQuery.oncommand = function(id,event){
	    
	var user = winform.editUser.text;
	var	pwd = winform.editPwd.text;
	
	if(!#user){
		return winform.editUser.showErrorTip("错误 - 未指定用户名","请指定用户名。使用微信注册用户名以后，才能在微信公众号查询处理结果");
	}
	
	if(!#pwd){
		return winform.editPwd.showErrorTip("错误 - 未指定密码","请指定用户密码。"); 
	}

	if(#user && #pwd){
		var ret = report.query(user=user,pwd=pwd)
		if(!ret){
			winform.msgboxErr("网络错误")
			return;
		}
		 
		if(ret.message){
			import string.markdown;
			var markdown = string.markdown()
			var html = markdown.render( ret.message );
			
			
			var frmWeb = win.form(text="反馈回复",parent = winform)
			
			import web.form;
			var wb = web.form(frmWeb);
			wb.noScriptErr = true;
			
			wb.html = /**
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title></title>
<script>
!function(e,t){"function"==typeof define&&define.amd?define(t):"object"==typeof exports?module.exports=t():t()}(0,function(){var o="details",i="summary";(function(){var e=document.createElement(o);if(!("open"in e))return!1;e.innerHTML="<"+i+">a</"+i+">b",document.body.appendChild(e);var t=e.offsetHeight;e.open=!0;var n=t!=e.offsetHeight;return document.body.removeChild(e),n})()||(document.documentElement.className+=" no-details",window.addEventListener("click",function(e){if("summary"===e.target.nodeName.toLowerCase()){var t=e.target.parentNode;if(!t)return;t.getAttribute("open")?(t.open=!1,t.removeAttribute("open")):(t.open=!0,t.setAttribute("open","open"))}}),function(e,t){if(document.getElementById(e))return;var n=document.createElement("style");n.id=e,n.innerHTML=t,document.getElementsByTagName("head")[0].appendChild(n)}("details-polyfill-style","html.no-details "+o+":not([open]) > :not("+i+") { display: none; }\nhtml.no-details "+o+" > "+i+':before { content: "▶"; display: inline-block; font-size: .8em; width: 1.5em; }\nhtml.no-details '+o+"[open] > "+i+':before { content: "▼"; }'))});
//# sourceMappingURL=/sm/553b125cee29bdb941065007e0203c54a036857cf6e81a93ee381477b77eeaf7.map
</script>
<style type="text/css">
body {
    font-family: Helvetica, arial, sans-serif;
    font-size: 10.5pt;
    line-height: 1.6;
    padding-top: 10px;
    padding-bottom: 10px;
    background-color: white;
    padding: 30px; 
}

body > *:first-child {
    margin-top: 0 !important; 
}

body > *:last-child {
    margin-bottom: 0 !important; 
}

details summary {
    cursor:pointer; 
     font-size: 12pt; 
}

details p{
  white-space:pre-line;  
}

a {
    color: #4183C4; 
}

a.absent {
    color: #cc0000; 
}

a.anchor {
    display: block;
    padding-left: 30px;
    margin-left: -30px;
    cursor: pointer;
    position: absolute;
    top: 0;
    left: 0;
    bottom: 0; 
}
h1, h2, h3, h4, h5, h6 {
	line-height: 1.5em;
    margin: 20px 0 10px;
    padding: 0;
    font-weight: bold;
    -webkit-font-smoothing: antialiased;
    cursor: text;
    position: relative; 
}
h1:hover a.anchor, h2:hover a.anchor, h3:hover a.anchor, h4:hover a.anchor, h5:hover a.anchor, h6:hover a.anchor {
    text-decoration: none; 
}
h1 tt, h1 code {
    font-size: inherit; 
}
h2 tt, h2 code {
    font-size: inherit; 
}
h3 tt, h3 code {
    font-size: inherit; 
}
h4 tt, h4 code {
    font-size: inherit; 
}
h5 tt, h5 code {
    font-size: inherit; 
}
h6 tt, h6 code {
    font-size: inherit; 
}
h2 {
    font-size: 18pt;
    color: black; 
    border-bottom: none;
	padding-bottom: 3px;
	text-align:center;
}
h3 {
    font-size: 13.5pt;
    padding-bottom: 3px;
    border-bottom: 3px solid #eee;
    color: #444;
}
h4 {
    font-size: 12.7pt; 
    border-bottom: 2px solid #ccc;
    color: #555;
}
h5 {
    font-size: 16px; 
    border-bottom: 1px solid #ccc;
    color: #666;
}
h6 {
    font-size: 15px; 
    border-bottom: 1px dotted #ccc;
    color: #777;
}
h7 {
    font-size: 14px; 
    border-bottom: 1px dotted #793862;
    color: #793862;
}
p, blockquote, ul, ol, dl, li, table, pre {
    margin: 15px 0; 
}
hr {
    border: 0 none;
    color: #cccccc;
    height: 4px;
    padding: 0; 
}

body > h2:first-child {
    margin-top: 0;
    padding-top: 0; 
}
body > h1:first-child {
    margin-top: 0;
    padding-top: 0;
}
body > h1:first-child + h2 {
    margin-top: 0;
    padding-top: 0; 
}
body > h3:first-child, body > h4:first-child, body > h5:first-child, body > h6:first-child {
    margin-top: 0;
    padding-top: 0; 
}
a:first-child h1, a:first-child h2, a:first-child h3, a:first-child h4, a:first-child h5, a:first-child h6 {
    margin-top: 0;
    padding-top: 0; 
}
h1 p, h2 p, h3 p, h4 p, h5 p, h6 p {
    margin-top: 0; }

li p.first {
    display: inline-block; 
}
ul, ol {
    padding-left: 30px; 
}
ul :first-child, ol :first-child {
    margin-top: 0; 
}
ul :last-child, ol :last-child {
    margin-bottom: 0; 
}
dl {
    padding: 0; 
}
dl dt {
    font-size: 14px;
    font-weight: bold;
    font-style: italic;
    padding: 0;
    margin: 15px 0 5px; 
}
dl dt:first-child {
    padding: 0; 
}
dl dt > :first-child {
    margin-top: 0; 
}
dl dt > :last-child {
    margin-bottom: 0; 
}
dl dd {
    margin: 0 0 15px;
    padding: 0 15px; 
}
dl dd > :first-child {
    margin-top: 0; 
}
dl dd > :last-child {
    margin-bottom: 0; 
}
blockquote {
    border-left: 4px solid #dddddd;
    padding: 0 15px;
    color: #777777; 
}
blockquote > :first-child {
    margin-top: 0; 
}
blockquote > :last-child {
    margin-bottom: 0; 
}
table {
    padding: 0; 
}
table tr {
    border-top: 1px solid #cccccc;
    background-color: white;
    margin: 0;
    padding: 0; 
}
table tr:nth-child(2n) {
    background-color: #f8f8f8; 
}
table tr th {
    font-weight: bold;
    border: 1px solid #cccccc;
    text-align: left;
    margin: 0;
    padding: 6px 13px; 
}
table tr td {
    border: 1px solid #cccccc;
    text-align: left;
    margin: 0;
    padding: 6px 13px; 
}
table tr th :first-child, table tr td :first-child {
    margin-top: 0; 
}
table tr th :last-child, table tr td :last-child {
    margin-bottom: 0; 
}
img {
    max-width: 100%; 
}
span.frame {
    display: block;
    overflow: hidden; 
}
span.frame > span {
    border: 1px solid #dddddd;
    display: block;
    float: left;
    overflow: hidden;
    margin: 13px 0 0;
    padding: 7px;
    width: auto; 
}
span.frame span img {
    display: block;
    float: left; 
 }
span.frame span span {
    clear: both;
    color: #333333;
    display: block;
    padding: 5px 0 0; 
}
span.align-center {
    display: block;
    overflow: hidden;
    clear: both; 
}
span.align-center > span {
    display: block;
    overflow: hidden;
    margin: 13px auto 0;
    text-align: center; 
}
span.align-center span img {
    margin: 0 auto;
    text-align: center; 
 }
span.align-right {
    display: block;
    overflow: hidden;
    clear: both; 
}
span.align-right > span {
    display: block;
    overflow: hidden;
    margin: 13px 0 0;
    text-align: right; 
}
span.align-right span img {
    margin: 0;
    text-align: right; 
}
span.float-left {
    display: block;
    margin-right: 13px;
    overflow: hidden;
    float: left; 
}
span.float-left span {
    margin: 13px 0 0; 
}
span.float-right {
    display: block;
    margin-left: 13px;
    overflow: hidden;
    float: right; 
}
span.float-right > span {
    display: block;
    overflow: hidden;
    margin: 13px auto 0;
    text-align: right; 
}
code, tt {
    margin: 0 2px;
    padding: 0 5px;
    white-space: nowrap;
    border: 1px solid #eaeaea;
    background-color: #f8f8f8;
    border-radius: 3px; 
}
pre code {
    margin: 0;
    padding: 0;
    white-space: pre;
    border: none;
    background: transparent; 
}
.highlight pre {
    background-color: #f8f8f8;
    border: 1px solid #cccccc;
    font-size: 13px;
    line-height: 19px;
    overflow: auto;
    padding: 6px 10px;
    border-radius: 3px; 
}
pre {
    background-color: #f8f8f8;
    border: 1px solid #cccccc;
    font-size: 13px;
    line-height: 19px;
    overflow: auto;
    padding: 6px 10px;
    border-radius: 3px; 
}
pre code, pre tt {
    background-color: transparent;
    border: none; 
}
</style>
</head>
<body>
</body>
</html>	 
**/

			wb.body.innerHTML = html;
			
			frmWeb.doModal();
			
			winform.lnkQuery.text = "您的反馈已收到回复，点这里查看！"
		}
		elseif(ret.error) {
			winform.msgboxErr(ret.error)
		} 
		else {
			if(ret.status===null){
				winform.msgbox("你在近期未提交任何反馈");
			}
			else {
				winform.msgbox("您的反馈正在处理中，请耐心等待回复。")
			}
			
		}
		
	} 
}

winform.lnkHelp.skin({
	color={
		active=0xFF00FF00;
		default=0xE5000080;
		disabled=0xFF6D6D6D;
		hover=0xFFFF0000		
	}
})

winform.lnkQuery.skin({
	color={
		active=0xFF00FF00;
		default=0xE5000080;
		disabled=0xFF6D6D6D;
		hover=0xFFFF0000		
	}
})

winform.show();
win.loopMessage();