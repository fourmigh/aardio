//表格控件
//文档指南: https://www.aardio.com/zh-cn/doc/library-guide/ext/dotNet/ReoGrid/
import win.ui;
/*DSG{{*/
var winform = win.form(text="ReoGrid - Excel 表格控件";right=757;bottom=467)
winform.add()
/*}}*/

import dotNet.ReoGrid; //导入库
var grid = ReoGrid.ReoGridControl(winform);//创建控件
var sheet1 = grid.CurrentWorksheet;//当前工作表

//通过行列多项索引修改第一行第一列
sheet1[0, 0] = time.now(); 

//使用 AI 格式下标修改第一行第一列
sheet1.Cells["A1"].Data = "测试";

//用函数设置列数据，参数（行索引、列索引，数据），注意 .NET 起始下标为 0
sheet1.SetCellData(5, 2, "hello world");

//直接读写 aardio 数组（减少不必要的交互，速度更快）
sheet1["B2:D4"] =  [ 
  [ "测试", "测试2" ],
  [ "测试3", "测试4" ],
];

//设置行列范围内的单元格数据
sheet1["B3:C5"] =  [ [ 'a', 'b', 'c' ], [ 1, 2, 3 ], [ 4, 5, 6 ] ];

//获取 Range（单元格集合） 对象
var range = sheet1.Ranges["D4:I4"];

//设置 Range 数据
range.Data = [ "Product", "Unit Price", "Quantity", "Extended Price"]; 

//修改样式（背景颜色） ，参数必须是 ReoGrid.Graphics.SolidColor 对象
range.Style.BackColor = ReoGrid.Graphics.SolidColor.Orange

//高亮区域
sheet1.AddHighlightRange("B2:D4");

///自定义控件
var checkboxCell = ReoGrid.CellTypes.CheckBoxCell();

//响应事件，不用任何封装，aardio 自动支持。
checkboxCell.CheckChanged = function(send,event){
	winform.msgbox(checkboxCell.IsChecked)
}
sheet1["B6"] = checkboxCell;

//在单元格中显示图像
import System.Drawing;
var img = System.Drawing.Image.FromFile("~\example\Graphics\.gdip.jpg")
var imgCell = ReoGrid.CellTypes.ImageCell(img);//创建图像控件
sheet1["B7"] = imgCell;

//保存文件
grid.Save("/test.xlsx");

winform.show();
win.loopMessage();