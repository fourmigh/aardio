//plus 控件 - 动态边框
//plus 控件使用指南: doc://library-guide/std/win/ui/ctrl/plus.md
import win.ui;
/*DSG{{*/
var winform = win.form(text="plus - 动态边框";right=759;bottom=469)
winform.add(
plus={cls="plus";text="鼠标移到这里按下试试";left=180;top=116;right=447;bottom=168;bgcolor=0xA4A0A0;notify=1;z=1};
plusVertical={cls="plus";text="鼠标移到这里按下试试";left=602;top=200;right=648;bottom=399;bgcolor=0xA4A0A0;notify=1;z=2}
)
/*}}*/

winform.plus.skin(

	//指定 border 在不同状态下的样式
	border = { 
		/*
		可选用 width 字段指定所有边框（左上右下）宽度，
		也可以用 left,top,right,bottom 分别指定左，上，右，下边框宽度。
		
		可选用 color 字段指定所有边框（左上右下颜色，
		也可以用 leftColor,topColor,rightColor,bottomColor 分别指定左，上，右，下边框颜色
		
		可选用 padding 字段指定边框两头的空白边距（可用于缩小边框高度）。
		
		可选用 radius 字段指定圆角半径，如果值为 -1 则将前景裁剪为圆形（不影响背景），并忽略其他边框样式。
		请参考圆形按钮范例: file://~/example/plus/round.aardio
		*/
		hover = {bottom=5;color=0xFFFF0000;padding=15;} 
		active = {right=5;color=0xFF00FF00;padding=15;}  
		default = {width=0}
	}
)

//设置 plus 控件的文本垂直显示（从上到下显示）
winform.plusVertical.stringFormatFlags = 2/*_StringFormatFlagsDirectionVertical*/;

winform.plusVertical.skin(
	border = { 
		hover = {top=5;color=0xFFFF0000;padding=10;} 
		active = {bottom=5;color=0xFF00FF00;padding=10;}  
		default = {width=0} 
	}
)

winform.show() 
win.loopMessage();