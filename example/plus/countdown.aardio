//用 plus 控件创建倒计时动画按钮
//plus 控件使用指南: doc://library-guide/std/win/ui/ctrl/plus.md
//倒计时消息框: file://~/example/Windows/Timer/msgCountdown.aardio
//自绘动画: file://~/example/Graphics/animation.aardio

import win.ui;
/*DSG{{*/
var winform = win.form(text="plus 控件 - 倒计时动画按钮";right=757;bottom=467)
winform.add(
plus={cls="plus";text="发送短信验证码";left=177;top=212;right=334;bottom=251;notify=1;z=1}
)
/*}}*/

/*
可选在 onAnimation 事件中更改控件属性（非必须，先执行此事件然后重绘控件），
或者在 onDrawContent 等重绘事件中定时绘制控件外观。
*/
winform.plus.onAnimation = function(state){ 
	
    winform.plus.disabledText = state + " 秒后重新获取"
    
    //倒计时结束
    if(!state) {
    	winform.plus.disabledText = null; 
    	return; //返回 null 停止动画
    }
   
	return  state-1;
}

	
winform.plus.oncommand = function(){ 
	//立即执行动画，并且定时继续执行动画
	winform.plus.startAnimation(1000,3) 
}

winform.plus.skin({
	background={
		default=0xFFA8C4C3;
		hover=0xFFD4A748;
		disabled=0xFFD4D4D4;
	}; 
})

winform.show() 
win.loopMessage();