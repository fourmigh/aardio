//IDE初始化触发器
//下面的代码在aardio开发环境初始完成以后触发
//修改下面的代码会导致开发环境无法正常工作

import win;
import ide;
import ide.command;

ide.command.join( ide.processGroup() );
var command = ide.command();
command.dispatchIdeCommand = function( method,...) {
	return ide[method](...);
}

thread.invoke( 
	function(){
		import win.version;
		import web.rest.jsonClient;	
		import ide;
		import ide.command;
		ide.command.join( ide.processGroup() );
		
		sleep(500);
		
		import fsys;
		fsys.enum( "~/", "*.dll",
			function(dir,filename,fullpath,findData){ 
				if(filename){ 
					import win;
					import winex;
					var hIde = ide.getMainHwnd();
					
					//篡改 aardio 开发环境并在网络上传播者一切后果自负。
					win.msgboxErr('aardio 根目录检测到非法的第三方 DLL 文件。IDE 即将退出！',,hIde);
					winex.close(hIde);
				}
			},false
		);

		sleep(1000);
		
		var cfgVersion = ide.getConfig("version");
		var http = web.rest.jsonClient("aardio/v" + _AARDIO_VERSION + "(IDE "  + cfgVersion + "," + win.version.name + ")");
		var versionInfo = http.get("http://api.aardio.com/v10.update/");
		if( ! versionInfo ) return;
		
		if(  ( cfgVersion != versionInfo.version )  && ( ide.getConfig("ignore_update") != versionInfo.version)  ){  
			import fsys.version;
			if( fsys.version(cfgVersion ) >= fsys.version( versionInfo.version ) ){
				return;
			}
		
			ide.startpage();
			import thread.event;
			var event = thread.event( ide.processGroup(".startPage") ) 
			if( !event.conflict ){
				event.waitOne();
			}
		
			ide.command.post("onUpdateAvailable",versionInfo.version );
		} 
	}
)

import win.reg;
var reg = win.reg("HKEY_CURRENT_USER\SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\aardio.exe");
reg.setSzValue("",..io._exepath);
reg.close();

win.loopMessage();